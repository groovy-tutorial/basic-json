plugins {
    id "org.asciidoctor.convert" version "1.5.2"
    id "org.ajoberstar.github-pages" version "1.3.1"
}

defaultTasks 'syncPublishFiles'

asciidoctor {
    logDocuments = true
    separateOutputDirs = false
}

task syncPublishFiles(type: Sync, dependsOn: [asciidoctor]) {
    description 'Collates all of the required files for publishing to GitHub pages branch'
    group 'publishing'

    mustRunAfter clean

    into "$buildDir/publish-site"

    from "${asciidoctor.outputDir}/"

    from(projectDir) {
        includes = ['build.gradle',
                    'settings.gradle',
                    'gradle.properties',
                    'gradlew',
                    'gradlew.bat',
                    'gradle/**',
                    'LICENSE',
                    '.nojekyll',
                    'run',
                    'run.bat']
    }

    from('src/main/groovy') {
        into 'src/main/groovy'
    }
}

task publish(type: GradleBuild, dependsOn: [syncPublishFiles]) {
    description 'Publishes to gh-pages'
    buildFile = 'publish.gradle'
    tasks = ['publishGhPages']
}

githubPages {
    repoUri = 'git@github.com:groovy-tutorial/basic-json.git'
    pages {
        from "$buildDir/publish-site"
    }
    publishGhPages {
        dependsOn syncPublishFiles
    }
}
